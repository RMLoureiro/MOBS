(** The type representing a node's credentials, generated by sortition. *)
type credential = Credential of node_id * block_id * num_selections * priority * params
  and node_id = Node of int (** id of the node *)
  and block_id = Block of int (** block used to run sortition *)
  and num_selections = Selections of int (** number of nodes selected by sortition *)
  and priority = Priority of int (** the priority for this node *)
  and params = Params of int list (** user-defined list of integer parameters used by sortition *)

(** Abstraction for common sortition and verification operations in proof of stake protocols. *)
module type PoS = sig

  (** Perform stake-based sortition, returning a list with the ids of the selected nodes.
    @param block the block whose balances will be used by sortition
    @param num_selections the number of nodes sortition will select
    @param params a list of integers that identify the selection (seed for the RNG-based abstraction)
  *)
  val sortition : 'a Simulator.Block.t -> int -> int list -> int list

  (** Check if a given node was selected by sortition to be part of the committee.
    @param node_id the id of the node
    @param block the block whose balances will be used by sortition
    @param committee_size the size of the committee
    @param params a list of integers that identify the selection (seed for the RNG-based abstraction)
  *)
  val in_committee : int -> 'a Simulator.Block.t -> int -> int list -> credential option

  (** Check if a given node was selected by sortition to be part of the committee.
    @param node_id the id of the node
    @param block the block whose balances will be used by sortition
    @param proposer_count the number of different proposers
    @param params a list of integers that identify the selection (seed for the RNG-based abstraction)
  *)
  val is_proposer : int -> 'a Simulator.Block.t -> int -> int list -> credential option

  (** Check if the given credentials belong to a valid proposer.
    @param node_id the id of the node
    @param block the block whose balances will be used by sortition
    @param proposer_count the number of different proposers
    @param params a list of integers that identify the selection (seed for the RNG-based abstraction)
    @param credential the node's credentials
  *)
  val valid_proposer : int -> 'a Simulator.Block.t -> int -> int list -> credential -> bool

  (** Check if the given credentials belong to a valid committee member.
    @param node_id the id of the node
    @param block the block whose balances will be used by sortition
    @param committee_size the size of the committee
    @param params a list of integers that identify the selection (seed for the RNG-based abstraction)
    @param credential the node's credentials
  *)
  val valid_committee_member : int -> 'a Simulator.Block.t -> int -> int list -> credential -> bool

  (** Obtain the priority associated with a node that was selected to propose a block.
    @param node_id the id of the node
    @param block the block whose balances will be used by sortition
    @param proposer_count the number of different proposers
    @param params a list of integers that identify the selection (seed for the RNG-based abstraction)
    @param credential the node's credentials
  *)
  val proposer_priority : int -> 'a Simulator.Block.t -> int -> int list -> credential -> int


end


module Make(Logger : Simulator.Logging.Logger)(Block : Simulator.Block.BlockSig) : PoS = struct

  let sortition block num_selections params =
    let r_state = Random.get_state () in
    let selected_nodes : int list ref = ref [] in
    let num_selected = ref 0 in
    let balances = Array.of_list (Block.balances block) in
    let total_coins = Block.total_coins block in
    Random.full_init (Array.of_list params);
    while !num_selected < num_selections do
      let weight = Random.float total_coins in
      let weight_sum = ref 0. in
      let selected = ref (-1) in
      for i=0 to (Array.length balances)-1 do
        if !selected = -1 then
          begin
            let (id, bal) = Array.get balances i in
            weight_sum := !weight_sum +. bal;
            if weight <= !weight_sum then selected := id
          end
      done;
      if not (List.exists (fun x -> x = !selected) !selected_nodes) then
        begin
          selected_nodes := !selected_nodes @ [!selected];
          num_selected := !num_selected + 1
        end
    done;
    Random.set_state r_state;
    !selected_nodes

  let get_credential node_id head num_selections params =
    let selections = sortition head num_selections params in
    if List.exists (fun id -> id = node_id) selections then
    (
      let res = ref (-1) in
      List.iteri (fun i id -> if id=node_id then res := i) selections;
      let prio = !res in
      Some(Credential(Node(node_id), Block(Simulator.Block.id head), Selections(num_selections), Priority(prio), Params(params)))
    )
    else
      None

  let valid_proposer node_id head num_proposers params credential = 
    match credential with
    | Credential(Node(id),Block(block_id),Selections(num_selections),Priority(_),Params(p)) -> 
      node_id = id && (Simulator.Block.id head) = block_id && num_proposers = num_selections && params = p

  let valid_committee_member node_id head committee_size params credential =
    match credential with
    | Credential(Node(id),Block(block_id),Selections(num_selections),Priority(_),Params(p)) -> 
      node_id = id && (Simulator.Block.id head) = block_id && committee_size = num_selections && params = p

  let in_committee node_id head committee_size params =
    match get_credential node_id head committee_size params with
    | Some(cred) -> Logger.print_in_committee node_id ((Block.height head)+1); Some(cred)
    | _ -> None

  let is_proposer node_id head num_proposers params =
    match get_credential node_id head num_proposers params with
    | Some(cred) -> Logger.print_is_proposer node_id ((Block.height head)+1); Some(cred)
    | _ -> None

  let proposer_priority node_id head num_proposers params credential =
    if valid_proposer node_id head num_proposers params credential then
    (
      match credential with
      | Credential(_,_,_,Priority(prio),_) -> prio
    )
    else
      -1

end
